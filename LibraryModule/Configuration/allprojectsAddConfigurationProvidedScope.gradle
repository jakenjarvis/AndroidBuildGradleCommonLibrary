// allprojectsAddConfigurationProvidedScope.gradle
def thisname = "allprojectsAddConfigurationProvidedScope"
println "Loading ${thisname}"

gradle.afterProject {eventproject, projectState ->
    allprojects { project ->
        def providedExists = false
        try {
            providedExists = (project.configurations.getAt("provided") != null)
        } catch(UnknownConfigurationException e) {
            providedExists = false
        }
        println ":${project.name}:${thisname} Provided scope exists? ${providedExists}"

        if ((!providedExists) && (!projectState.failure)) {
            println ":${project.name}:${thisname} Start"

            def javaProject = project.plugins.hasPlugin('java')
            def groovyProject = project.plugins.hasPlugin('groovy')

            def androidApplicationProject = project.plugins.hasPlugin('android')
            def androidLibraryProject = project.plugins.hasPlugin('android-library')
            def androidProject = androidApplicationProject || androidLibraryProject

            def mavenProject = project.plugins.hasPlugin('maven')

            def ideaProject = project.plugins.hasPlugin('idea')
            def eclipseProject = project.plugins.hasPlugin('eclipse')

            project.configurations {
                provided
                provided.extendsFrom(compile)
            }

            // http://stackoverflow.com/questions/18738888/how-to-use-provided-scope-for-jar-file-in-gradle-build
            if(javaProject || groovyProject || androidProject) {
                println ":${project.name}:${thisname} Detected a Java|Groovy|Android project."
                project.sourceSets {
                    main.compileClasspath += project.configurations.provided
                    test.compileClasspath += project.configurations.provided
                    test.runtimeClasspath += project.configurations.provided
                }
                println ":${project.name}:${thisname} Added the provided scope."
            }

            // https://groups.google.com/forum/#!topic/adt-dev/WIjtHjgoGwA
            if(androidApplicationProject) {
                println ":${project.name}:${thisname} Detected a Android Application project."
                project.android.applicationVariants.all {
                    variant -> variant.javaCompile.classpath += project.configurations.provided
                }
                println ":${project.name}:${thisname} Added the provided scope classpath."
            }
            if(androidLibraryProject) {
                println ":${project.name}:${thisname} Detected a Android Library project."
                project.android.libraryVariants.all {
                    variant -> variant.javaCompile.classpath += project.configurations.provided
                }
                println ":${project.name}:${thisname} Added the provided scope classpath."
            }

            // http://www.gradle.org/docs/current/userguide/maven_plugin.html
            // http://www.gradle.org/docs/current/javadoc/org/gradle/api/artifacts/maven/Conf2ScopeMappingContainer.html
            if(mavenProject) {
                println ":${project.name}:${thisname} Detected a Maven project."
                Conf2ScopeMappingContainer scopeMappings = project.conf2ScopeMappings
                scopeMappings.addMapping(MavenPlugin.PROVIDED_COMPILE_PRIORITY, project.configurations.provided, Conf2ScopeMappingContainer.PROVIDED)
            }

            // http://www.gradle.org/docs/current/dsl/org.gradle.plugins.ide.idea.model.IdeaModule.html#org.gradle.plugins.ide.idea.model.IdeaModule:scopes
            // http://gradle.1045684.n5.nabble.com/Idea-project-generation-and-provided-scope-td4684287.html
            // http://gradle.org/docs/2.0/release-notes#+=-operator-changes-that-typically-affect-configuration-of-eclipse-and-idea-plugin
            if(ideaProject) {
                println ":${project.name}:${thisname} Detected a IDEA project."
                project.idea.module {
                    // TODO: idea plugin seems to be incomplete when it is automatically loaded by the Android Studio. Timing is bad?
                    //       Gradle 'projectname' project refresh failed: Cannot get property 'plus' on null object
                    try {
                        scopes.PROVIDED.plus += [project.configurations.provided]

                        //scopes.PROVIDED.plus += project.configurations.provided.compile
                        //scopes.PROVIDED.plus += project.configurations.provided.runtime
                        //scopes.COMPILE.minus += project.configurations.provided.compile
                        //scopes.RUNTIME.minus += project.configurations.provided.runtime
                    } catch(NullPointerException e) {
                        println ":${project.name}:${thisname} ${e}"
                    }
                }
                println ":${project.name}:${thisname} Added the provided scope classpath."
            }

            // http://qiita.com/k_ui/items/edfce12a2fac8fbfec04
            // http://www.gradle.org/docs/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseClasspath.html
            if(eclipseProject) {
                println ":${project.name}:${thisname} Detected a Eclipse project."
                project.eclipse.classpath {
                    plusConfigurations += project.configurations.provided
                    noExportConfigurations += project.configurations.provided
                }
                println ":${project.name}:${thisname} Added the provided scope classpath."
            }

            println ":${project.name}:${thisname} End"
        }
    }
}
