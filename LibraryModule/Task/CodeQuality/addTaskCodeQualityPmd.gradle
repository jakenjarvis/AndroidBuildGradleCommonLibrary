// addTaskCodeQualityPmd.gradle
def thisname = "addTaskCodeQualityPmd"
println "Loading ${thisname}"

// http://www.gradle.org/docs/current/userguide/pmd_plugin.html

def javaProject = project.plugins.hasPlugin('java')
def androidApplicationProject = project.plugins.hasPlugin('android')
def androidLibraryProject = project.plugins.hasPlugin('android-library')

if(androidApplicationProject || androidLibraryProject) {
    def variants
    def variantsTypeName
    if(androidApplicationProject) {
        variants = android.applicationVariants
        variantsTypeName = "Android Application"
    }
    if(androidLibraryProject) {
        variants = android.libraryVariants
        variantsTypeName = "Android Library"
    }

    println ":${project.name}:${thisname} Detected a ${variantsTypeName} project."
    apply plugin: 'pmd'
    println ":${project.name}:${thisname} apply plugin: pmd"

    afterEvaluate { project ->
        variants.all { variant ->
            def name = variant.name.capitalize()
            def taskname = "pmd${name}"

            def task = project.tasks.create(taskname, Pmd)
            task.source files(variant.sourceSets.allJava.srcDirs)

            // https://github.com/pmd/pmd/tree/master/pmd/src/main/resources/rulesets/java

            def checktask = project.tasks.getByName('check')
            checktask.dependsOn(task)

            println ":${project.name}:${thisname} Register ${taskname}"
        }
    }
}

if(javaProject) {
    println ":${project.name}:${thisname} Detected a Java project."
    apply plugin: 'pmd'
    println ":${project.name}:${thisname} apply plugin: pmd"
}
