// addTaskCodeQualityFindbugs.gradle
def thisname = "addTaskCodeQualityFindbugs"
println "Loading ${thisname}"

// http://www.gradle.org/docs/current/userguide/findbugs_plugin.html

def javaProject = project.plugins.hasPlugin('java')
def androidApplicationProject = project.plugins.hasPlugin('android')
def androidLibraryProject = project.plugins.hasPlugin('android-library')

if(androidApplicationProject || androidLibraryProject) {
    def variants
    def variantsTypeName
    if(androidApplicationProject) {
        variants = android.applicationVariants
        variantsTypeName = "Android Application"
    }
    if(androidLibraryProject) {
        variants = android.libraryVariants
        variantsTypeName = "Android Library"
    }

    println ":${project.name}:${thisname} Detected a ${variantsTypeName} project."
    apply plugin: 'findbugs'
    println ":${project.name}:${thisname} apply plugin: findbugs"

    afterEvaluate { project ->
        variants.all { variant ->
            def name = variant.name.capitalize()
            def taskname = "findbugs${name}"

            def task = project.tasks.create(taskname, FindBugs)

            //task.classes = files(variant.sourceSets.main.output.classesDir)
            task.classes = fileTree("${buildDir}/classes/${variant.name}")

            task.source files(variant.sourceSets.allJava.srcDirs)
            task.include '**/*.java'
            task.exclude '**/gen/**'
            task.classpath = files()

            def checktask = project.tasks.getByName('check')
            checktask.dependsOn(task)

            println ":${project.name}:${thisname} Register ${taskname}"
        }
    }
}

if(javaProject) {
    println ":${project.name}:${thisname} Detected a Java project."
    apply plugin: 'findbugs'
    println ":${project.name}:${thisname} apply plugin: findbugs"
}
