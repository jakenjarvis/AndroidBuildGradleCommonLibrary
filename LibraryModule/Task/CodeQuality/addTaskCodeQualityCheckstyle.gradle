// addTaskCodeQualityCheckstyle.gradle
def thisname = "addTaskCodeQualityCheckstyle"
println "Loading ${thisname}"

// http://www.gradle.org/docs/current/userguide/checkstyle_plugin.html

def javaProject = project.plugins.hasPlugin('java')
def androidApplicationProject = project.plugins.hasPlugin('android')
def androidLibraryProject = project.plugins.hasPlugin('android-library')

if(androidApplicationProject || androidLibraryProject) {
    def variants
    def variantsTypeName
    if(androidApplicationProject) {
        variants = android.applicationVariants
        variantsTypeName = "Android Application"
    }
    if(androidLibraryProject) {
        variants = android.libraryVariants
        variantsTypeName = "Android Library"
    }

    println ":${project.name}:${thisname} Detected a ${variantsTypeName} project."
    apply plugin: 'checkstyle'
    println ":${project.name}:${thisname} apply plugin: checkstyle"

    afterEvaluate { project ->
        variants.all { variant ->
            def name = variant.name.capitalize()
            def taskname = "checkstyle${name}"

            def task = project.tasks.create(taskname, Checkstyle)
            task.source files(variant.sourceSets.java.srcDirs)
            task.include '**/*.java'
            task.exclude '**/gen/**'
            task.classpath = files()

            def checktask = project.tasks.getByName('check')
            checktask.dependsOn(task)

            println ":${project.name}:${thisname} Register ${taskname}"
        }
    }
}

if(javaProject) {
    println ":${project.name}:${thisname} Detected a Java project."
    apply plugin: 'checkstyle'
    println ":${project.name}:${thisname} apply plugin: checkstyle"
}
